/* partio4Maya  3/12/2012, John Cassella  http://luma-pictures.com and  http://redpawfx.com
PARTIO Export
Copyright 2012 (c)  All rights reserved

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are
met:

* Redistributions of source code must retain the above copyright
notice, this list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright
notice, this list of conditions and the following disclaimer in
the documentation and/or other materials provided with the
distribution.

Disclaimer: THIS SOFTWARE IS PROVIDED BY  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING,
BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE, NONINFRINGEMENT AND TITLE ARE DISCLAIMED.
IN NO EVENT SHALL  THE COPYRIGHT HOLDER OR
CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND BASED ON ANY
THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGES.
*/

global proc AEpartioVisualizerTemplate( string $nodeName )
{
	editorTemplate -callCustom pioVizLogoHeader pioVizLogoHeader $nodeName;
	editorTemplate -beginLayout "Global" -collapse 0;
	editorTemplate -addControl "cacheActive";
	editorTemplate -addControl "staticCache";
	editorTemplate -addControl "flipYZ";
	editorTemplate -callCustom pioVizAEoutpathNew pioVizAEoutpathReplace cacheDir;
	editorTemplate -addControl "cachePrefix";
	editorTemplate -addControl "cacheOffset";
	editorTemplate -addControl "cachePadding";
	editorTemplate -addControl "cacheFormat";
	editorTemplate -addControl "iconSize";
	editorTemplate -callCustom pioVizForceUpdate pioVizForceUpdate $nodeName;


	editorTemplate -endLayout;

	editorTemplate -beginScrollLayout;

		editorTemplate -beginLayout "General Parameters" -collapse 0;
			editorTemplate -addControl "pointSize";
			editorTemplate -addControl "drawStyle";
			editorTemplate -addControl "drawSkip";
			editorTemplate -addControl "defaultPointColor";
			editorTemplate -addControl "defaultAlpha";
			editorTemplate -addControl "invertAlpha";
		    //editorTemplate -addControl "useEmitterTransform";
		    //editorTemplate -addControl -l "Jitter Position Magnitude" "jitterPos" ;
		    //editorTemplate -addControl -l "Jitter Position Frequency" "jitterFreq";
		editorTemplate -endLayout;
		editorTemplate -beginLayout "Channel Control" -collapse 0;
		editorTemplate -callCustom "pioVizCustomAttrsManage" "pioVizCustomAttrsManage" $nodeName;
		editorTemplate -endLayout;

	// include/call base class/node attributes
	AEdependNodeTemplate $nodeName;

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}


global proc int pioVizAEassignCacheFileCB( string $fileAttribute,
									 string $fileName,
									 string $fileType )
{
	string $components[];
  	tokenize $fileAttribute "." $components;
  	string $nodeName = $components[0];

  	string $fileNameComp[];

	string $baseFileName = basename($fileName,"");

	tokenize $baseFileName "." $fileNameComp;

	string $fileBaseName = $fileNameComp[0];

	string $padding = $fileNameComp[size($fileNameComp)-2];
	string $ext = $fileNameComp[size($fileNameComp)-1];

	stringArrayRemoveAtIndex(size($fileNameComp)-1,$fileNameComp);
	stringArrayRemoveAtIndex(size($fileNameComp)-1,$fileNameComp);

	string $fileBaseName = stringArrayToString($fileNameComp, ".");

	setAttr ($nodeName + ".cachePrefix") -type "string" $baseFileName;

	int $pad = size($padding);

	int $extIndex = 0;

	switch ($ext)
	{
		case "bgeo":
			$extIndex = 0;
			break;
		case "geo":
			$extIndex = 1;
			break;
		case "pda":
			$extIndex = 2;
			break;
		case "pdb":
			$extIndex = 3;
			break;
		case "pdc":
			$extIndex = 4;
			break;
		case "mc":
			$extIndex = 5;
			break;
		case "bin":
			$extIndex = 6;
			break;
		case "prt":
			$extIndex = 7;
			break;
		case "ptc":
			$extIndex = 8;
			break;
		case "pts":
			$extIndex = 9;
			break;
		case "rib":
			$extIndex = 10;
			break;
		case "ass":
			$extIndex = 11;
			break;
		default:
			$extIndex = 4;
			break;

	}

	setAttr ($nodeName + ".staticCache") 0;
	if ($ext == "pdc")
	{
		$pad = 0;
	}

	else if ($ext == "mc")
	{
		$pad = 0;
	}
	else if ($ext == "pts")
	{
		setAttr ($nodeName + ".staticCache") 1;
		$pad = 0;
	}

	setAttr ($nodeName + ".cachePadding") $pad;
	setAttr ($nodeName + ".cacheFormat") $extIndex;

	$fileName = substituteAllString($fileName, basename($fileName, ""), "");
  	setAttr $fileAttribute -type "string" $fileName;

	// This will force an update to display the effects
	// of the new cachefile.
	//
	currentTime `currentTime -q`;

  string $currentDir = `workspace -q -dir`;
  retainWorkingDirectory ($currentDir);

  return true;
}

global proc pioVizAEcacheFileBrowser( string $cmd, string $cacheName )
{
    string  $workspace = `workspace -q -fn`;
    setWorkingDirectory $workspace "diskCache" "data";

    fileBrowser ($cmd, "Partio Cache Directory", "", 0);
}

global proc pioVizAEoutpathNew(string $cacheName)
{
	setUITemplate -pst attributeEditorTemplate;
		columnLayout -adj true;
			rowLayout -nc 3 outpathLayout;
        		text -label "Particle Path" outpathText;
		        textField diskNameField;
		            symbolButton -image "navButtonBrowse.png" browser;
				setParent ..;

		setParent ..;

	setUITemplate -ppt;
	setParent ..;

	pioVizAEoutpathReplace( $cacheName );
}

global proc pioVizAEoutpathReplace(string $cacheName)
{
	string $fileName = `getAttr $cacheName`;
	string	$buffer[];
	tokenize($cacheName, ".", $buffer);
	string	$nodeName = $buffer[0];

	// Filename Attribute
  connectControl diskNameField $cacheName;
  string $command = "pioVizAEassignCacheFileCB " + " " + $cacheName;
  button -e -c ("pioVizAEcacheFileBrowser \"" + $command + "\" " + $nodeName ) browser;
}


global proc string pioVizStripNodeName (string $nodeName) {
	string $buffer[];
	tokenize $nodeName "." $buffer;
	return $buffer[0];
}

global proc pioVizCustomAttrsManage (string $nodeName)
{
    $nodeName = pioVizStripNodeName ($nodeName);
    if (`columnLayout -q -exists "foobar"`)
    { deleteUI "foobar"; }

    columnLayout  foobar;

    string $multiList[] = `listAttr -multi ($nodeName+".partioCacheAttributes")`;

	int $colorFrom = `getAttr ($nodeName+".colorFrom")` + 2;
	int $opacityFrom = `getAttr ($nodeName+".opacityFrom")` + 2;
	int $radiusFrom  = `getAttr ($nodeName+".radiusFrom")` + 2;


	string $colorMenu = `optionMenuGrp -label "Color from->"`;
	menuItem -l "None";

    for ($multi in $multiList)
    {
		string $tok[];
		tokenize $multi "[" $tok;
		string $index = $tok[1];
		$index = substituteAllString($index, "]", "");
		string $attr = `getAttr ($nodeName+"."+$multi)`;
       	 menuItem -label $attr;
    }
	optionMenuGrp -e -cc ("pioVizSwitchAttributeFrom " + $nodeName + " " + 0 + " " + $colorMenu)  $colorMenu;


    string $opacMenu = `optionMenuGrp -label "Opacity from->"`;
	menuItem -l "None";
    for ($multi in $multiList)
    {
		string $tok[];
		tokenize $multi "[" $tok;
		string $index = $tok[1];
		$index = substituteAllString($index, "]", "");
		string $attr = `getAttr ($nodeName+"."+$multi)`;
       	 menuItem -label $attr;
    }
    optionMenuGrp -e -cc ("pioVizSwitchAttributeFrom " + $nodeName + " " + 1 + " " + $opacMenu)  $opacMenu;

	/*
	string $radiusMenu = `optionMenuGrp -label "Radius from->"`;
	menuItem -l "None";
    for ($multi in $multiList)
    {
		string $tok[];
		tokenize $multi "[" $tok;
		string $index = $tok[1];
		$index = substituteAllString($index, "]", "");
		string $attr = `getAttr ($nodeName+"."+$multi)`;
       	 menuItem -label $attr;
    }
	optionMenuGrp -e -cc ("pioVizSwitchAttributeFrom " + $nodeName + " " + 2 + " " + $radiusMenu)  $radiusMenu;
	*/

	if (`size($multiList)`)
	{
		optionMenuGrp -e -sl $colorFrom  $colorMenu;
		optionMenuGrp -e -sl $opacityFrom $opacMenu;
		//optionMenuGrp -e -sl $radiusFrom $radiusMenu;
	}

}

global proc pioVizSwitchAttributeFrom(string $nodeName, int $attr, string $menu)
{
	string $attrList[] = {"colorFrom", "opacityFrom", "radiusFrom"};
	int $sel = `optionMenuGrp -q -sl $menu`;

	setAttr ($nodeName+"."+$attrList[$attr]) ($sel-2);

}

global proc pioVizForceUpdate(string $nodeName)
{
	$nodeName = pioVizStripNodeName ($nodeName);
	if (!`button -q -exists  pioVizReloadButton`)
	{	rowLayout -nc 1 ;
		button -ebg true -bgc 1 0.678 0.125 -w 100 -l "---<Force Reload>---"  -c ("setAttr "+$nodeName+".forceReload  true") pioVizReloadButton;
	}
}

global proc  pioVizLogoHeader(string $nodeName)
{
	if (!`symbolButton -q -exists  pioVizHeaderIcon`)
	{	rowLayout -nc 2 ;
		symbolButton -h 45 -w 235 -c "pioVizSwapAboutText()" -image "partioVisualizer_logo.png" pioVizHeaderIcon;
		text -fn "smallBoldLabelFont" -l "Partio Visualizer!!" pioVizAboutText;
	}
}


global proc  pioVizSwapAboutText()
{

	string $text1 =  "Partio Visualizer!!";
	string $text2 =  "Partio Visualizer by: \n John Cassella  (redpawfx)\n  for Luma-Pictures  2012";

	string $text = `text -q -l pioVizAboutText`;
	if (`gmatch $text "Partio Visualizer!!"`)
	{
		text -e -l $text2  pioVizAboutText;
	}
	else
	{
		text -e -l $text1 pioVizAboutText;
	}

}




